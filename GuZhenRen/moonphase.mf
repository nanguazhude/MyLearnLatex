% moonphase.mf
% by Stanislav Brabec, utx@k332.feld.cvut.cz

font_size=10pt#;
mode_setup;
design_size=10pt#;

path octcircle,fiveoctcircle,profile,peye,leye,reye,nose,lips;
octcircle=(right{up}..(right+up)/sqrt2) scaled .5;
fiveoctcircle = halfcircle rotated -22.5 & octcircle rotated 157.5;
profile = (0pt,9.5pt)---(0pt,7.5pt)..(-.5pt,6pt)..(-.8pt,4.7pt)..
  (0pt,4.5pt)---(.5pt,4.6pt)--
  (0pt,4.5pt)--(0pt,2.5pt)--(1.3pt,2.7pt)--(0pt,2.5pt)--(.2pt,.5pt);
peye = (1pt,7.5pt)..{5*right+up}(2pt,7.3pt);
leye = (3pt,7.4pt)..{5*right+up}(4pt,7.3pt);
reye = (6pt,7.3pt){5*right+down}..(7pt,7.4pt);
nose =
(5pt,6.5pt)---(5pt,5.8pt)..(4.5pt,4.2pt)..(5pt,3.9pt)---(5.2pt,3.9pt);
lips = (4pt,2.5pt)..(5pt,2.4pt)..(6pt,2.5pt);

"New Moon";
beginchar(0,10pt#,10pt#,0);
fill fullcircle scaled 10pt shifted (5pt,5pt);
pickup pencircle scaled .75pt;
undraw leye;
undraw reye;
undraw nose;
undraw lips;
endchar;

"Open Moon";
beginchar(1,7.8pt#,10pt#,0);
pickup pencircle scaled .8pt;
draw fiveoctcircle scaled 9.2pt rotated -90 shifted (2.2pt,5pt);
pickup pencircle scaled .5pt;
draw profile shifted (2.2pt,0pt);
draw peye shifted (2.2pt,0pt);
endchar;

"Full Moon";
beginchar(2,10pt#,10pt#,0);
pickup pencircle scaled .7pt;
draw fullcircle scaled 9.3pt shifted (5pt,5pt);
pickup pencircle scaled .5pt;
draw leye;
draw reye;
draw nose;
draw lips;
endchar;

"Close Moon";
beginchar(3,7.8pt#,10pt#,0);
pickup pencircle scaled .8pt;
draw fiveoctcircle scaled 9.2pt rotated 90 shifted (5pt,5pt);
pickup pencircle scaled .5pt;
draw profile reflectedabout((0,0),(0,1)) shifted (5pt,0pt);
draw peye reflectedabout((0,0),(0,1)) shifted (5pt,0pt);
endchar;

end.
